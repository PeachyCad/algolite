<цифра> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
<буква> ::= /[A-Za-z]/
<число> ::= <цифра> [{<цифра>} ['.' <цифра> [{<цифра>}]]]
<логическое значение> ::= 'true' | 'false'
<имя> ::= <буква> [{<буква> | <цифра>}]
<переменная> := <имя> [{'[' <выражение> ']'}]
<тип данных> ::= 'int' | 'bool' | 'long' | 'long long' | 'double' | <вектор>
<вектор> ::= 'vector' '<' <тип данных> '>' 
<сумма> ::= <слагаемое> [{<аддитивный оператор> <слагаемое>}]
<and-выражение> ::= <сумма> | <условие>
<or-выражение> ::= <and-выражение> [{'&&' <and-выражение>}]
<выражение> ::= <or-выражение> [{'||' <or-выражение>}]
<слагаемое> ::= <множитель> [{<мультипликативный оператор> <множитель>}]
<аддитивный оператор> ::= '+'|'-'
<мультипликативный оператор> ::= '*' | '/' | '%'
<множитель> ::= ['-'] <атом>
<атом> ::= <число> | <логическое значение> | <переменная> | <вызов функции> | <вызов метода вектора> | '(' <выражение> ')'
<аргумент функции> ::= <тип данных> <имя> [{'[' <выражение> ']'}]
<знак> ::= '>=' | '<=' | '>' | '<' | '==' | '!='
<условие> ::= <сумма> <знак> <сумма>
<объявление функции> ::= (<тип данных> | 'void') <имя> '(' [<аргумент функции> [{',' <аргумент функции>}]] ')' (';' | <определение функции>)   
<определение функции> ::= '{' <команда> '}'                                               
<вызов функции> ::= (<имя> '(' [<выражение> [{ ',' <выражение> }]] ')') | <вызов системной функции>
<вызов системной функции> ::= 'rand' '(' ')' 
<вызов метода вектора> ::= <добавления элемента в конец вектора> | <удаление элемента с конца вектора> | <размер вектора>
<добавления элемента в конец вектора> ::= <переменная> '.' 'push_back' '(' <выражение> ')'
<удаление элемента с конца вектора> ::= <переменная> '.' 'pop_back' '(' ')'
<размер вектора> ::= <переменная> '.' 'size' '(' ')'
<объявление переменной> ::= <тип данных> <переменная> ['=' <выражение>] [{',' <переменная> ['=' <выражение>]}]
<объявление параметра сложности> ::= 'parameter' <имя> '(' <выражение> ',' <выражение> ',' <выражение> ')' ';'
<режим подсчета средней сложности> ::= '# AVERAGE_COMPLEXITY_MODE' <число> '#' ';'
<объявление числа потоков для составления функции сложности> ::= '# COMPLEXITY_FUNCTION_FILL_THREADS' <число> '#' ';'
<присваивание значения переменной> ::= <переменная> '=' <выражение> 
<инкремент> ::= (<переменная> '++') | ('++' <переменная>) 
<декремент> ::= (<переменная> '--') | ('--' <переменная>) 
<break> ::= 'break'
<continue> ::= 'continue' 
<return> ::= 'return' <выражение>
<уменьшение значения> ::= <переменная> '-=' <выражение> 
<увеличение значения> ::= <переменная> '+=' <выражение>
<отключение анализа в блоке> ::= '# ANALYSIS_DISABLE #' (<блок> - <отключение анализа в блоке>) 
<if> ::= 'if' '(' <выражение> ')' <команда> ['else' <команда>]
<while условие> ::= 'while' '(' <выражение> ')'
<while> ::= <while условие> <команда>
<do while> ::= 'do' <команда> <while условие>
<for> ::= 'for' '(' [<простая команда>] ';' [<выражение>] ';' [<простая команда>] ')' <команда>  
<case> ::= 'case' <выражение> ':' [{<команда>}]
<default> ::= 'default' ':' [{<команда>}]
<switch> ::= 'switch' '(' <выражение> ')' '{' [{<case>}] [<default>] '}'
<печать> ::= 'cout' '<<' (<выражение> | <строковый литерал>) [{'<<' (<выражение> | <строковый литерал>)}]
<блок> ::= '{' [{<команда>}] '}' 
<простая команда> ::= <вызов функции> | <вызов метода вектора> | <объявление переменной> | <присваивание значения переменной> | <добавления элемента в конец вектора> | <удаление элемента с конца вектора> | <печать> | <инкремент> | <декремент> | <уменьшение значения> | <увеличение значения> | <break> | <continue> | <return>
<команда> ::= (<простая команда> ';') | (<do while> ';') | <отключение анализа в блоке> | <if> | <for> | <switch> | <while> | <блок>
<инициализация> ::= <объявление параметра сложности> [{<объявление параметра сложности>}] [<режим подсчета средней сложности>] [<объявление числа потоков для составления функции сложности>]
<программа> ::= <инициализация> [{<объявление функции>}]